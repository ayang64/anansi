
<h1>NaviServer nsv Commands</h1>

The nsv commands provide a high performance data sharing mechanism.
This facility is much flexible alternative to the obsolete ns_share
command.  The model uses an array syntax and includes more features.
In addition, lock contention is managed in a much more scalable
way--something that is not possible with the obsolete ns_share
facility.

<h2>Basics</h2>

<p>

The following commands currently make up the nsv interface:

<p>

    	nsv_get - get key value
<p>
    	nsv_exists - check key existence
<p>
    	nsv_set - set key value
<p>
    	nsv_append - append value
<p>
    	nsv_lappend - append value as list element
<p>
    	nsv_incr - increment and return value
<p>
    	nsv_unset - unset a value
<p>
    	nsv_array - manage nsv arrays
<p>

<p>

Commands for the most part mirror the cooresponding Tcl command for
ordinary variables.  Basically, to set a value, simply use the nsv_set
command:

<p>

<pre.

	nsv_set myarray foo $value
</pre>

<p>

and to get a value, simply use the nsv_get command:

<p>

<pre>

	set value [nsv_get myarray foo]

</pre>

<p>



<p>

<h2>Migrating From ns_share</h2>

<p>

Migrating from ns_share is straightforward.  If your init.tcl included commands such as:

<p>

<pre.

	ns_share myshare
	set myshare(lock) [ns_mutex create]
</pre>

<p>

use instead:

<p>

<pre>

	nsv_set myshare lock [ns_mutex create]
</pre>

<p>

In your procedures, instead of:

<p>

<pre>

	proc myproc {} {
		ns_share myshare
		ns_mutex lock $myshare(lock)
		...

</pre>

<p>

use:

<p>

<pre>


	proc myproc {} {
		ns_mutex lock [nsv_get myshare lock]
		...

</pre>

<p>

and within an ADP page, instead of:

<p>

<pre>


	&lt;%
	ns_share myshare
	ns_puts $myshare(key1)
	%&gt;

	&lt;%=$myshare(key2)%&gt;

</pre>

<p>

use:

<p>

<pre>

	&lt;%
	ns_puts [nsv_get myshare key1]
	%&gt;

	&lt;%=[nsv_get myshare key2]%&gt;

</pre>

<p>


Notice that, unlike ns_share, no command is required to define the
shared array.  The first attempt at setting the variable through any
means will automaticaly create the array.  Also notice that only arrays
are supported.  However, to migrate from ns_share you can simply package
up all existing ns_share scalars into a single array with a short name,
perhaps just ".". For example, if you had:

<p>

<pre>

	ns_share mylock myfile
	set myfile /tmp/some.file
	set mylock [ns_mutex create]

</pre.

<p>

you can use:

<p>

<pre>
	nsv_set . myfile /tmp/some.file
	nsv_set . mylock [ns_mutex create]

</pre>

<p>


<h2>Multithreading Features</h2>

<p>

One advantages of nsv is built in interlocking for thread safety.
For example, consider a case of a "increment-by-one" unique id system.
Here's the ns_share solution:

<pre>

	ns_share ids
	set ids(lock) [ns_mutex create]
	set ids(next) 0

	proc nextid {} {
		ns_share ids
		ns_mutex lock $ids(lock)
		set next [incr ids(next)]
		ns_mutex unlock $ids(lock)
		return $next
	}

</pre>

and here's an nsv solution:

<pre>
	nsv_set ids next 0

	proc nextid {} {
		return [nsv_incr ids next]
	}

</pre>

Note that the nsv solution does not need a mutex as the nsv_incr command
is internally interlocked.

<p>


<h2>Compatibility with Tcl Arrays</h2>

<p>

Another useful feature of nsv is the nsv_array command which works much
like the Tcl array command.  This can be used to import and export values
from ordinary Tcl arrays.  For example, to copy from Tcl use:

<p>

	nsv_array set meta [array get tmpmeta]

<p>

and to copy to Tcl use:

<p>

	array set metacopy [nsv_array get meta]

<p>

As with all other nsv command, nsv_array is atomic and no explicit
locking is required.  This feature can be used to contruct a new nsv
array by first filling up an ordinary temporary Tcl array via some time
consuming process and then swapping it into place as above.  While the
new temporary array is being constructed, other threads can access the
old array without delay or inconsistant data.  You can even reset a
complete nsv array in one step with "reset".  For example, instead of:

<pre>
	ns_share lock meta
	set lock [ns_mutex create]
	ns_mutex lock $lock
	unset meta
	array set meta [array get tmpmeta]
	ns_mutex unlock $lock
</pre>

you can simply use:

<p>

	nsv_array reset meta [array get tmpmeta]

<p>

The reset option will flush and then reset all values atomically,
eliminating the need for the explicit lock.

<p>

Other options for the nsv_array command include:

<p>

	nsv_exists array - test existance of array
<p>
	nsv_size array - return # of elements in array
<p>
	nsv_names array - return keys of array

<p>


<h2>Configuration</h2>

<p>

The nsv system uses a common multithreading technique to reduce the
potential for lock contention which is to split the locks to acheive
finer grained locking.  This technique groups arrays randomly into
buckets and only the arrays within a particular bucket share a lock.
The number of buckets to be used can be configured by setting the
"nsvbuckets" tcl parameters, e.g.:

<p>

<pre>

	[ns/server/server1/tcl]
        nsvbuckets=20
</pre>

<p>

The default is 8 which should be reasonalbe.  Note that you can monitor
the lock contention, if any, by enabling mutex metering:

<p>

<pre>
	[ns/threads]
        mutexmetering=on
</pre>
<p>

and then viewing the results of "ns_info locks" command after the server
has been running for some time.  The nsv locks all have names of the
form "nsv:##".  If you find many lock attempts which did not successed
immediately, try increasing nsvbuckets.



