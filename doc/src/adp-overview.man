[manpage_begin {NaviServer ADP Development} n 4.99.2]

[titledesc {NaviServer ADP Development}]

[description]

[section {What Are ADPs?}]


[para]


Probably the easiest way to make NaviServer output dynamic content is
to embed a Tcl script in an HTML page with NaviServer Dynamic Pages
(ADPs). ADPs are HTML pages that are parsed and run on the server when
the page is accessed. ADPs contain HTML tags and Tcl scripts that are
embedded within the HTML tags. The embedded Tcl scripts can call other
Tcl scripts that reside in separate files, allowing you to reuse Tcl
code.


[para]



[section {When to Use ADPs}]


[para]


ADPs are ideal in situations where you want to generate all or part of
a specific page dynamically. You can re-use code by storing Tcl
scripts in Tcl libraries and invoking them from within multiple ADPs.
You can also include files and parse other ADPs from within your ADPs.


[para]


Here are some examples of applications you can use ADPs for:


[para]


[list_begin bullet]

[bullet] Returning HTML conditionally


[nl]


[bullet] Retrieving information from a database to use in a page


[nl]


[bullet] Inserting information from a form into a database


[list_end]



[para]



[section {When to Use Tcl Libraries}]


[para]


The alternative to embedding Tcl scripts in HTML pages using ADPs, is
to store Tcl scripts in Tcl libraries and register them to handle
specific URLs or URL hierarchies. There are some situations, such as
those listed below, that are better suited to the Tcl libraries
approach.


[para]


[list_begin bullet]

[bullet] Inheritance: If you want one Tcl script to handle an URL and all
 of its sub-URLs, it's better to store the script in a Tcl library
 and register it using ns_register_proc to handle an URL hierarchy.
 For example, you may want to manage a server domain name change by
 redirecting every response to the corresponding domain name on
 another server.


[nl]


[bullet] Special Extenstions: If you want one Tcl script to handle all
 files with a specific extension, like /*.csv, you would register
 the script with ns_register_proc to handle those files.


[nl]


[bullet] Scheduled Procedures: If you want a Tcl script to be run at
 specific intervals, you can use the ns_schedule_* functions to run
 a script from the Tcl library at scheduled intervals. These
 procedures do not normally involve returning HTML pages and so are
 not well suited to ADPs.


[nl]


[bullet] Filters: If you want a Tcl script to be called at
 pre-authorization, post-authorization, or trace time for a group
 of URLs, you would register a filter using the ns_register_filter
 function.


[nl]


[bullet] Re-using Tcl Scripts: If there are Tcl scripts that you want to
 use in multiple situations, you can store them in a Tcl library
 and invoke them from within any ADP or Tcl script.


[list_end]



[para]



[section {Configuring ADP Processing}]


[para]


Since you will be creating HTML pages that contain Tcl scripts, you
will need to specify which pages the server will need to parse for Tcl
commands and process.


[para]


 [subsection {Required Configuration Parameters}]


[para]


[list_begin bullet]


[bullet] Use the Map configuration parameter to determine
 which files are to be processed. For example, you can specify that
 all files with the .adp extension are to be processed by the
 server. Or, you can specify that all files that reside in a
 certain directory (for example, /usr/pages/adp) will be processed.


[nl]


[bullet] If you want to register your own ADP tags, you must set the Fancy
 parameter to On. It also allows you to use the <server> ...
 </server> ADP syntax and determine whether ADP tags can be
 registered after startup.


[list_end]



[para]


 [subsection {List of Configuration Parameters}]


[para]


The following table describes all the parameters that can be set
within the ADP section of the configuration file:


[para]


Section [emph {ns/server/servername/adp}]:


[para]


[list_begin bullet] [emph {Cache}] - If set to on, ADP caching is enabled.


[nl]


Deafult: on


[nl]


[bullet] [emph {DebugInit}] - The procedure to run when debugging begins.


[nl]


Deafult: ns_adp_debuginit


[nl]



[bullet] [emph {EnableExpire}] - If set to on, the "Expires: now" header is set on all
     outgoing ADPs


[nl]


Default: off


[nl]


[bullet] [emph {EnableCompress}] - If set to on, extraneous spaces within an HTML page are removed


[nl]


Default: off


[nl]


[bullet] [emph {EnableDebug}] - If set to on, appending "?debug" to a URL will enable TclPro debugging


[nl]


Deafult: off


[nl]


[bullet] [emph {Fancy}] - If set to on, "fancy" ADPs are enabled, meaning that:
     the TagLocks parameter is enabled the <server>...</server> syntax is
     enabled the ns_register_adptag and ns_adp_registertag commands are
     enabled registered ADP tags are enabled


[nl]


Deafult: off


[nl]


[bullet] [emph {Map}] - The Map parameter specifies which pages the server will parse. You can
     specify a file extension (such as /*.adp) or a directory (such as
     /usr/pages/adp). If no directory is specified, the pages directory is
     assumed. The wildcards * ? and [] can be included in the Map
    s pecification. You can specify multiple Map settings.


[nl]


The following example specifies that all files in the pages directory
with the extensions .adp or .asp will be parsed, and all files in the
/usr/pages/myadps directory will be parsed.


[example_begin]

 Map "/*.adp"
 Map "/*.asp"
 Map "/usr/pages/myadps"

[example_end]



[nl]


[bullet] [emph {StartPage}]  - The file to be run on every connection instead of the requested ADP.
     It can be used to perform routine initialization. It would then
     usually include the requested ADP by calling:
     ns_adp_include [ns_adp_argv 0]


[nl]


[bullet] [emph {TagLocks}] - If set to on, ADP tags may be registered after server
     startup. Pages will be served less quickly when this is turned on.
     If set to off, ADP tags can only be registered before the first ADP is served.


[nl]


Default: off


[list_end]



[para]



[section {Building ADPs}]


[para]



[list_begin enum]


[enum] Create an HTML page. Use an HTML editor or a file editor to create
 an HTML page. Be sure to either name the file with the correct
 extension or save it in the correct directory on your server as
 specified by the Map parameter setting.


[nl]


  If you plan to use the Tcl script to return part or all of the
 page's content, just omit that part of the page, but you
 can create all of the surrounding HTML.


[nl]


[enum] Add your Tcl scripts with a file editor. Insert your Tcl scripts
 in the HTML file where you want them to be processed. Be sure to
 enclose each Tcl script using one of the <SCRIPT> or <% ...%>
 syntaxes . Save the HTML file.


[nl]


[enum] View the HTML page in a browser. Visit the page you have created
 in a browser to see if the Tcl scripts work correctly. If you have
 set the EnableDebug parameter, you can append "?debug" to the URL
 to enable TclPro debugging.


[nl]


[enum] Continue editing and viewing until it works correctly. Continue
 editing the page in a file editor, saving it, and refreshing it in
 a browser until it works the way you want it to.


[list_end]



[para]



[section {Debugging ADPs with TclPro}]


[para]


To debug your ADPs with TclPro, follow these steps:


[para]


[list_begin bullet]

[bullet] Download and install TclPro from http://www.scriptics.com/tclpro/.
 Temporary licenses are available.

[bullet] Copy the prodebug.tcl file from the TclPro distribution to the
 modules/tcl directory.

[bullet] Set the EnableDebug parameter in the ns/server/servername/adp
 section of the configuration file to On.

[bullet] Run TclPro and start a "remote project", which puts TclPro into a
 mode waiting for NaviServer to connect.

[bullet] Open an ADP file with the ?debug=<pattern> query string in
 addition to any other query data you may send. NaviServer will then
 trap on ADP files matching the pattern. For example, you can just
 use debug=*.adp to trap all files, or you can use
 debug=someinclude.inc file to trap in an included file.


[list_end]



[para]


  To set a breakpoint in a procedure you'll have to "instrument"
 the procedure either after the debugger traps the first
 time or with the "dproc=<pattern>" query argument.


[para]



[section {ADP Syntax}]


[para]


There are three different syntaxes you can use to embed a Tcl script
into an HTML page. Not all syntaxes are available with all ADP
parsers. You must be using the appropriate ADP parser to process a
specific syntax.


[para]


Insert Tcl commands between any of the following sets of HTML tags:


[para]


[list_begin bullet]

[bullet] <script language=tcl runat=server stream=on>


[nl]


  ...


[nl]


  </script>


[nl]


You must have the Fancy parameter set to On to use this
syntax. The contents of the script are interpreted using the Tcl
interpreter. The result is not inserted into the page, however. You
can use the ns_puts Tcl function to put content into the page.


[nl]


The language=tcl attribute is optional. Future enhancements to ADPs
will include the capability to embed scripts in other scripting
languages, and you will be able to configure the default language. If
the language attribute is set to anything except tcl, the script will
not be processed, and a warning will be written to the log file.


[nl]


The runat=server attribute is required. If this attribute is missing,
the script will not be processed. The runat=server attribute is
necessary so that client-side scripts are not executed accidentally.


[nl]


The stream=on attribute is optional. If it is included, all output for
the rest of the page is streamed out to the browser as soon as it's
ready. Streaming is useful when your script may take a long time to
complete (such as a complex database query). Content is output to the
page gradually as the script is running. One disadvantage of streaming
is that the server cannot return a Content-length header with the
response. Content-length can speed up the connection, especially if
the connection is going through a proxy server.


[nl]


[bullet] <% ... %>


[nl]


This syntax is evaluated exactly the same as the first syntax, above,
except that you cannot specify any of the attributes. The language=Tcl
and runat=server attributes are implied. Streaming is not allowed with
this syntax, but output within this syntax will be streamed if
streaming was turned on in a previous script. This syntax can be used
inside HTML tags.


[nl]


[bullet] <%= ... %>


[nl]


The Tcl commands within these tags are evaluated as the argument to an
ns_puts command, which inserts the results into the page. This syntax
can be used inside HTML tags.


[list_end]



[para]



[section {Registered ADP Tags}]


[para]


You can define your own ADP tags with the ns_adp_registertag or the
ns_register_adptag Tcl functions.


[para]


You must have the Fancy parameter set to On to use registered ADP
tags. You can also set the Taglocks parameter to prevent new
registered tags from being created after the first ADP has been
served.


[para]



[section {ADP Parsers}]


[para]


An ADP parser implements a specific syntax for ADPs. Two ADP parsers
are pre-defined for NaviServer:


[para]



[list_begin enum]


[enum] adp: The "adp" parser handles only the <% ... %> syntax and the
 <%= ... %> syntax for ADPs. This parser offers the best
 performance.
 fancy: The "fancy" parser handles the <script language=tcl
 runat=server stream=on> syntax in addition to the other two
 syntaxes. It also handles registered ADP tags. The TagLocks
 parameter is enabled, and the ns_register_adptag and
 ns_adp_registertag commands are enabled. This parser offers a
 moderate performance degradation as compared to the "adp" parser.


[nl]


You can register additional ADP parsers using the Ns_AdpRegisterParser
C API function.


[nl]


 Configuration


[nl]


To configure ADP parsers, set parameters in two configuration file
sections as follows:
 1. Map parsers to file extensions in the


[example_begin]

 ns/server/servername/adp/parsers configuration file section. For
 example:
 ns_section "ns/server/server1/adp/parsers"
 ns_param "adp" ".adp"
 ns_param "fancy" ".fadp"

[example_end]



[nl]


  The above configuration specifies that ADPs with the .adp
 extension will be parsed using the "adp" parser, and ADPs
 with the .fadp extension will be parsed using the "fancy"
 parser.


[nl]


[enum] Define a default parser in the ns/server/servername/adp section.
 The default parser will be used for any ADPs with file extensions
 not associated with a parser. For example:


[example_begin]

 ns_section "ns/server/server1/adp"
 ns_param "map" "/*.adp"
 ns_param "map" "/*.inc"
 ns_param "map" "/*.fadp"
 ns_param "DefaultParser" "adp"
 ns_section "ns/server/server1/adp/parsers"
 ns_param "adp" ".adp"
 ns_param "fancy" ".fadp"

[example_end]



[nl]


  ADPs with the .adp extension will use the "adp" parser, and ADPs
 with the .fadp extension will use the "fancy" extension,
 as described above. The default ADP parser will be the
 "adp" parser. Since ADPs with the .inc extension are also
 allowed, the "adp" parser will be used for those ADPs,
 because no parser is associated with the .inc extension.


[list_end]



[para]


For more information on configuring ADPs, see the ADP configuration
file sections starting on page page 56 of the NaviServer
Administrator's Guide.


[para]



[section {Example ADPs}]


[para]


This section contains the following ADP examples:


[para]


 Example 1: Return partial HTML page conditionally


[para]


 Example 2: Return full HTML page conditionally


[para]


 Example 3: Return information from the database


[para]


 Example 4: Get form information and insert into the database


[para]


 Example 5: ADP sampler with includes, recursion, and streaming



[para]


 Example 1: Return partial HTML page conditionally


[para]


This ADP example tests for various browsers and returns a different message in each case.


[para]



[example_begin]


 <%
 ns_puts "Hello"
 set ua [ns_set get [ns_conn headers] "user-agent"]
 ns_puts "$ua "
 if [string match "*MSIE*" $ua] {
  ns_puts "This is MS Internet Explorer"
 } elseif [string match "*Mozilla*" $ua] {
  ns_puts "This is Netscape or a Netscapecompatible browser"
 } else {
  ns_puts "Couldn't determine the browser"
 }
 %>


[example_end]



[para]


 Example 2: Return full HTML page conditionally


[para]


This example consists of a form, cookbook.html, that asks the user
whether they want to view a page with or without frames, and an ADP,
cookbook.adp, that determines the response and displays the
appropriate page, either the page with frames or the page without
frames.


[para]


This is the cookbook.html file containing the form:


[para]



[example_begin]


 <HTML>
 <HEAD><TITLE>The ABC's of Fruit Cookbook</TITLE></HEAD>
 <BODY BGCOLOR="#ffffff">
 <H1>The ABC's of Fruit Cookbook</H1>
 <P>
 How would you like to view this cookbook?
 <FORM ACTION="cookbook.adp" METHOD="POST">
 <INPUT TYPE="radio" NAME="question" VALUE="yes" CHECKED>With Frames<BR>
 <INPUT TYPE="radio" NAME="question" VALUE="no">Without Frames
 <P>
 <INPUT TYPE=submit VALUE="View Cookbook">
 </FORM>
 <P>
 </BODY>
 </HTML>


[example_end]



[para]



This is the ADP, cookbook.adp, that determines the response and
displays the appropriate page:


[para]



[example_begin]


 <HTML><HEAD><TITLE>The ABC's of Fruit Cookbook</TITLE></HEAD>
 <BODY BGCOLOR="#ffffff">
 <%
 # Get form data and assign to variables
 set r [ns_conn form $conn]
 set question [ns_set get $r question]
 # Display cookbook in appropriate format
 if {$question == "yes"} {ns_adp_include cookset.html} \
 else {ns_adp_include cook.html}
 %>
 </BODY></HTML>


[example_end]



[para]


The cookset.html file contains a frame set for the cookbook. The
cook.html file contains the cookbook without frames.


[para]



Example 3: Return information from the database


[para]


This example retrieves information from the database -- a list of
tables -- and returns it as the options in a select box. When the user
chooses a table from the list, another ADP is run as the POST for the
form which retrieves information from the database on the chosen
table.


[para]


The first ADP, db.adp, creates a form with a select box with the list
of database tables:


[para]



[example_begin]


 <HTML><HEAD><TITLE>DB Example</TITLE></HEAD>
 <BODY>
 <H1>DB Example</H1>
 <P>
 Select a db table from the default db pool:
 <FORM METHOD=POST ACTION=db2.adp>
 <SELECT NAME=Table>
 <%
 set db [ns_db gethandle]
 set sql "select * from tables"
 set row [ns_db select $db $sql]
 while {[ns_db getrow $db $row]} {
 set table [ns_set get $row name]
 ns_puts "<OPTION VALUE=\"$table\">$table"
 }
 %>
 </SELECT>
 <INPUT type=submit value="Show Data">
 </FORM>
 </BODY>
 </HTML>


[example_end]



[para]


The second ADP, db2.adp, is used as the POST from the first ADP:


[para]



[example_begin]


 <HTML><HEAD><TITLE>DB Example page 2</TITLE></HEAD>
 <BODY>
 <H1>DB Example page 2</H1>
 <%
 set table [ns_set get [ns_conn form $conn] Table]
 set db [ns_db gethandle]
 %>
 Contents of <%=$table%>:
 <Table border=1>
 <%
  set row [ns_db select $db "select * from $table"]
  set size [ns_set size $row]
  while {[ns_db getrow $db $row]} {
    ns_puts "<tr>"
    for {set i 0} {$i < $size} {incr i} {
      ns_puts "<td>[ns_set value $row $i]</td>"
    }
    ns_puts "</tr>"
  }
 %>
 </table>
 </BODY>
 </HTML>


[example_end]



[para]


 Example 4: Get form information and insert into the database


[para]


This is another database example, but one where the user types
information into a form, and the submit runs an ADP that enters the
information into the database. Then it sends an email message to both
the db administrator and the user that the record was updated. The
survey.html file contains the form and calls the survey.adp file as
the POST action.


[para]


Here is the survey.html file, which consists of a simple form and a
submit button which calls an ADP:


[para]



[example_begin]

 <HTML><HEAD><TITLE>Survey Form</TITLE>
 </HEAD>
 <BODY BGCOLOR="#ffffff">
 <H2>Online Newsletter Subscription</H2>
 <P>
 <I>Sign up to be notified when this web site changes, or to receive an ASCII version via email. Thanks!</I>
 <FORM ACTION="survey.adp" METHOD="POST">
 <B>Name</B>
 <INPUT TYPE="text" NAME="name" SIZE="40">
 <P>
 <B>Title </B>
 <INPUT TYPE="text" NAME="title" SIZE="40" MAXLENGTH="80">
 <P>
 <INPUT TYPE="checkbox" NAME="notify" VALUE="1">Notify me by email when this newsletter changes online
 <P>
 <INPUT TYPE="checkbox" NAME="sendemail" VALUE="1">Send me an ASCII version of this newsletter by email
 <P>
 <B>Email Address
 </B>
 <INPUT TYPE="text" NAME="emailaddr" SIZE="40" MAXLENGTH="60">
 <P>
 <INPUT TYPE=submit>
 </FORM>
 <P>
 </BODY>
 </HTML>


[example_end]



[para]


Here is the survey.adp file, which gets the form data from the survey,
inserts it into the database, sends email to the subscription
administrator and the user, and displays a confirmation message:


[para]



[example_begin]

 <HTML><HEAD><TITLE>Subscription Processed Successfully</TITLE>
 </HEAD>
 <BODY BGCOLOR="#ffffff">
 <H2>Online Newsletter Subscription</H2>
 <P>
 Thank You for subscribing to our newsletter!
 <%
 # Get form data and assign to variables
 set r [ns_conn form $conn]
 set name [ns_set get $r name]
 set title [ns_set get $r title]
 set notify [ns_set get $r notify]
 set sendemail [ns_set get $r sendemail]
 set emailaddr [ns_set get $r emailaddr]

 # Set subscription options explicity to 0 if not checked
 if {$notify != 1} {set notify 0}
 if {$sendemail != 1} {set sendemail 0}

 # Update database with new subscription
 set db [ns_db gethandle]
 ns_db dml $db "insert into test values ([ns_dbquotevalue $name],
                                         [ns_dbquotevalue $title], $notify, $sendemail,
                                         [ns_dbquotevalue $emailaddr])"

 # Send email message to subscription administrator
 set body "A new newsletter subscription was added for "
 append body $name
 append body ". The database has been updated."
 ns_sendmail "subscript@thecompany.com" "dbadmin@thecompany.com" "New Subscription" $body

 # Send email message to user
 set body "Your online newsletter subscription has been successfully processed."
 ns_sendmail $emailaddr "dbadmin@thecompany.com" "Your Online Subscription" $body

 # Show type of subscription to user
 if {$notify == 1} {
   ns_puts "You will be notified via email when the online newsletter changes."
 }
 if {$sendemail == 1} {
   ns_puts "Future issues of the newsletter will be sent to you via email."
 }
 %>
 </BODY>
 </HTML>


[example_end]



[para]



 Example 5: ADP sampler with includes, recursion, and streaming


[para]


The following HTML is an example of a page containing several Tcl
scripts using the various ADP syntaxes. It invokes some Tcl functions,
includes a file, executes another ADP, and uses streaming.


[para]



[example_begin]

 <HTML><HEAD><TITLE>This is a test of ADP</TITLE>
 </HEAD>
 <BODY>
 <H1>This is a test of ADP</H1>
 <%

 ## Proxies should cache this page for a maximum of 1 hour:
 ns_setexpires $conn 3600
 set host [ns_set iget [ns_conn headers $conn] host]

 ## How many times has this page been accessed
 ## since the server was started?
 ns_share -init {set count 0} count
 incr count
 %>

 Number of accesses since server start: <%=$count%><br>
 tcl_version: <%=$tcl_version%><br>
 tcl_library: <%=$tcl_library%><br>
 Host: <%= $host %><br>

 <!-- Include the contents of a file: -->
 <%
 ns_adp_include standard-header
 %>

 <script language=tcl runat=server>
 ## You can do recursive ADP processing as well:
 ns_adp_include time.adp
 </script>

 <P>Here's an example of streaming:
 <script stream=on runat="server">
 ns_puts "<br>1...<br>"
 ns_sleep 2
 ns_puts "2...<br>"
 ns_sleep 2
 ns_puts "3!<br>"
 </script>
 <p>
 <b>End</b>
 </BODY>
 </HTML>


[example_end]



[para]


The standard-header file referenced in the above example looks like this:


[para]


This is a standard header.


[para]


The time.adp file referenced in the example looks like this:


[para]


The time is: <%=[ns_httptime [ns_time]]%>


[para]


Because of the streaming used in the last script, the "1...", "2...",
"3!" and "End" part of the page will be displayed gradually.



[manpage_end]
